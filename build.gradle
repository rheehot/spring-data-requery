buildscript {

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/plugins-release" }
        maven { url "https://dl.bintray.com/kotlin/dokka/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"

        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "io.spring.gradle:propdeps-plugin:0.0.9.RELEASE"

        classpath "io.spring.gradle:propdeps-plugin:0.0.9.RELEASE"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"

        // for Java apt
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.15"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-noarg'
// for Java apt
apply plugin: "net.ltgt.apt"

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'idea'

apply plugin: 'maven'
apply plugin: 'maven-publish'

apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: "io.spring.dependency-management"

// apply plugin: 'org.junit.platform.gradle.plugin'

allOpen {
    annotation("javax.persistence.Entity")
    annotation("io.requery.Entity")
}
noArg {
    annotation("javax.persistence.Entity")
    annotation("io.requery.Entity")
    invokeInitializers = true
}

sourceSets {
    generated {
        "${buildDir}/generated/source/kapt/main/"
    }
}

idea {
    module {
        sourceDirs += file("${buildDir}/generated/source/kapt/main/")
        generatedSourceDirs += file("${buildDir}/generated/source/kapt/main/")
    }
}

group = 'com.coupang'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$spring_boot_version"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinx_coroutines_version"

    compile "org.springframework.data:spring-data-commons:2.0.7.RELEASE"

    compile "io.requery:requery:$requery_version"
    compile "io.requery:requery-kotlin:$requery_version"

    kapt "io.requery:requery-processor:$requery_version"
    kaptTest "io.requery:requery-processor:$requery_version"

    compile "org.slf4j:slf4j-api:1.7.25"
    testCompile "ch.qos.logback:logback-classic:1.2.3"
    testCompile "io.github.microutils:kotlin-logging:1.4.6"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"

    testCompile "org.assertj:assertj-core:3.9.1"
    testCompile "org.mockito:mockito-core:2.18.3"
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"

    // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
    testRuntime "org.junit.platform:junit-platform-launcher:$junit_platform_version"
    testRuntime "org.junit.platform:junit-platform-console-standalone:$junit_platform_version"

    // To avoid compiler warnings about @API annotations in JUnit code
    testCompileOnly "org.apiguardian:apiguardian-api:1.0.0"

    testCompile "org.springframework.boot:spring-boot-starter-jdbc:$spring_boot_version"
    testCompile "org.springframework.boot:spring-boot-starter-test:$spring_boot_version"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true
        apiVersion = "1.2"
        languageVersion = "1.2"
        suppressWarnings = false
    }
}

compileTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

test {
    useJUnitPlatform()
    // systemProperty 'java.util.logging.manager', 'ch.qos.logback.classic.Logger'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
    jdkVersion = 8
    //reportNotDocumented = false
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives dokkaJar
}

configurations {
    published
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "debop"
            name "Sunghyouk Bae"
            email "sunghyouk.bae@gmail.com"
        }
    }

    scm {
        url "https://github.com/debop/koda-time"
    }
}

publishing {
    publications {
        bintrayJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact dokkaJar

            groupId "${project.group}"
            artifactId "${project.name}"
            version "${project.version}"

            pom.withXml {
                def root = asNode()
                root.appendNode('description', "Kotlin extension functions for Java 8 Time and joda-time")
                root.appendNode('name', "${project.name}")
                root.appendNode("url", "https://github.com/debop/koda-time")
                root.children().last() + pomConfig
            }
        }
    }
}